// can be tested on https://www.lark_parser.org/ide/#
// notation is based on CHORD_TYPES in 
// https://github.com/cuthbertLab/music21/blob/fcb97b4266c78e4d547c9a41a5f818f5f379c79e/music21/harmony.py

?start: chord

// ignore whitespaces (no semantic meaning in chord annotations)
%import common.WS
%ignore WS
NOTE: "A".."G"
DEGREE: "2" | "3" | "4" | "5" | "6" | "7" | "9" | "11" | "12" | "13"
sharp: "#" | "#h"
flat: "b" | "h" | "bh"
?alter: sharp | flat
altered_note: NOTE alter?
altered_degree: alter? DEGREE

root: altered_note
chord: root shorthand degree_modification* slash?
polychord: chord chord*

shorthand: triad | seventh | sixth | ninth | eleventh | thirteenth | other

?triad: major | minor | augmented | diminished | minor_augmented
major: | "M" | "maj" | "^"
minor: "m" | "min" | "-"
augmented: "+" | "aug" | "7#5" | "ug"
minor_augmented: "-7#5" | "-#5"
diminished: "dim" | "o" | "im"

// sevenths
?seventh: dominant_seventh | major_seventh | minor_major_seventh | minor_seventh | augmented_major_seventh | augmented_seventh | half_diminished_seventh | diminished_seventh | diminished_seventh_major_seventh | seventh_flat_five | major_seventh_flat_five
dominant_seventh: "7" | "dom7" | "h7"
major_seventh: "maj7" | "M7" | "^7" | "aj7" | "7#"
minor_major_seventh: "mM7" | "m#7" | "minmaj7" | "-^7"
minor_seventh: "m7" | "min7" | "-7" | "im7"
augmented_major_seventh: "+M7" | "augmaj7" | "^7#5"
augmented_seventh: "7+" | "+7" | "aug7"
half_diminished_seventh: "ø7" | "-7b5" | "7b5"
diminished_seventh: "o7" | "dim7"
diminished_seventh_major_seventh: "o^7"
seventh_flat_five: "dom7dim5"
major_seventh_flat_five: "^7#5"


// sixths
?sixth: major_sixth | minor_sixth | minor_flat_sixth
major_sixth: "6"
minor_sixth: "m6" | "min6" | "-6"
minor_flat_sixth: "-b6"

// ninth
?ninth: major_ninth | dominant_ninth | minor_major_ninth | minor_ninth | augmented_major_ninth | augmented_dominant_ninth | half_diminished_ninth | half_diminished_minor_ninth | diminished_ninth | diminished_minor_ninth | flat_ninth | minor_flat_ninth | sharp_ninth | major_sharp_ninth | sixth_ninth | minor_sixth_ninth | sharp_7th_flat_5th_flat_9th | augmented_sharp_ninth | seventh_flat_9th_sharp_9th | seventh_flat_9th_sharp_5th | seventh_flat_9th_flat_5th | seventh_sharp_9th_flat_5th | flat_fifth_9th
major_ninth: "M9" | "Maj9" | "79#"
dominant_ninth: "9" | "dom9" | "79" | "^9" | "aj9"
minor_major_ninth: "mM9" | "minmaj9" | "-^9"
minor_ninth: "m9" | "min9" | "-9" | "-79"
augmented_major_ninth: "+M9" | "augmaj9" | "#5#9"
augmented_sharp_ninth: "7#9#5" | "+79#"
augmented_dominant_ninth: "9#5" | "+9" | "aug9"
half_diminished_ninth: "ø9"
half_diminished_minor_ninth: "øb9"
diminished_ninth: "o9" | "dim9"
diminished_minor_ninth: "ob9" | "dimb9"
flat_ninth: "7b9"
minor_flat_ninth: "-7b9"
sharp_ninth: "7#9"
major_sharp_ninth: "^7#"
sixth_ninth: "69"
minor_sixth_ninth: "-69"
sharp_7th_flat_5th_flat_9th: "#7b9b5"
seventh_flat_9th_sharp_9th: "7b9#9" | "b9b5"
seventh_sharp_9th_flat_5th: "7#9b5" | "#9b5"
seventh_flat_9th_flat_5th: "7b9b5" | "b9b5" | "b7b9b5"
seventh_flat_9th_sharp_5th: "7b9#5" | "b9#5"
flat_fifth_9th: "9b5" | "b7b9b5"

// elevenths
?eleventh: dominant_11th | major_11th | minor_major_11th | minor_11th | augmented_major_11th | augmented_11th | half_diminished_11th | diminished_11th | major_sharp_11th | augmented_sharp_11th | sharp_ninth_sharp_11th | sharp_11th | major_sharp_ninth_sharp_11th | minor_major_sharp_11th | sixth_ninth_sharp_eleventh | flat_ninth_sharp_11th | major_ninth_sharp_11th
dominant_11th: "11" | "dom11"
major_11th: "M11" | "Maj11"
minor_major_11th: "mM11" | "minmaj11"
minor_11th: "m11" | "min11" | "-7911" | "-7911" | "-11"
augmented_major_11th: "+M11" | "augmaj11" | "j7911#"
augmented_11th: "+11" | "aug11" | "7911#"
half_diminished_11th: "ø11"
diminished_11th: "o11" | "dim11"
major_sharp_11th: "^79#11" | "^7#11"
sharp_11th: "79#11" | "7#11" | "9#11"
augmented_sharp_11th: "+79#11"
sharp_ninth_sharp_11th: "7#9#11"
major_sharp_ninth_sharp_11th: "^7#9#11"
minor_major_sharp_11th: "-^79#11"
sixth_ninth_sharp_eleventh: "69#11"
flat_ninth_sharp_11th: "7b9#11" | "b9#11"
major_ninth_sharp_11th: "^9#11"

// thirteenth
?thirteenth: major_13th | dominant_13th | minor_major_13th | minor_13th | augmented_major_13th | augmented_dominant_13th | half_diminished_13th | sharp_ninth_13th | flat_13th | flat_ninth_flat_13th | flat_ninth_sharp_13th | flat_13th_sharp_11th | flat_ninth_13th | sharp_eleventh_13th | sharp_ninth_sharp_13th | sharp_ninth_flat_13th
major_13th: "M13" | "Maj13" | "^13"
dominant_13th: "13" | "dom13" | "7913" | "713"
minor_major_13th: "mM13" | "minmaj13"
minor_13th: "m13" | "min13"
augmented_major_13th: "+M13" | "augmaj13"
augmented_dominant_13th: "+13" | "aug13"
half_diminished_13th: "ø13"
sharp_ninth_13th: "7#913" | "13#9"
flat_ninth_sharp_13th: "#13b9"
flat_13th: "79b13" | "7b13"
flat_ninth_flat_13th: "7b9b13" | "b13b9"
flat_13th_sharp_11th: "b13#11"
flat_ninth_13th: "13b9"
sharp_eleventh_13th: "13#11"
sharp_ninth_sharp_13th: "7#9#13" | "#9#13" | "#13#9"
sharp_ninth_flat_13th: "7#9b13" | "b13#9" | "#9b13"

// other
?other: suspended_second | suspended_fourth | suspended_fourth_seventh | suspended_ninth | suspended_flat_ninth | suspended_thirteen | neapolitan | italian | french | german | pedal | power | tristan | alt | suspended_flat_thirteen
suspended_second: "sus2" | "2" | "us2"
suspended_fourth: "sus" | "sus4" | "sus" | "us" | "us4"
suspended_fourth_seventh: "7sus" | "7sus4" | "sus7" | "us7"
suspended_ninth: "sus79" | "sus9" | "9sus" | "us9"
suspended_flat_ninth: "7b9sus" | "usb9"
suspended_thirteen: "sus7913" | "sus13" | "13sus" | "us13"
suspended_flat_thirteen: "b7b13sus" | "7b13sus"
neapolitan: "N6"
italian: "It+6" | "It"
french: "Fr+6" | "Fr"
german: "Gr+6" | "Ger"
pedal: "pedal"
power: "power" | "5" | "-5"
tristan: "tristan"
alt: "alt" | "7alt" | "7at"

// degree modification
?degree_modification: subtract_degree | alter_degree | add_degree
subtract_degree: "subtract" altered_degree
alter_degree: "alter" altered_degree
add_degree: "add" altered_degree | "dd" altered_degree

// slash chord
slash: "/" altered_note